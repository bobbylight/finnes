plugins {
    id 'com.github.spotbugs' version '4.7.1'
}

group 'org.fifesoft'
version '0.1.0-SNAPSHOT'

allprojects {

    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }

    wrapper {
       gradleVersion = '7.1.1'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'

    checkstyle {
        toolVersion = '8.43'
        configDirectory = file("$rootProject.projectDir/config/checkstyle")
    }

    spotbugsMain {
        reports {
            html {
                enabled = true
            }
            xml {
                enabled = false
            }
        }
    }
    spotbugsTest {
        reports {
            html {
                enabled = true
            }
            xml {
                enabled = false
            }
        }
    }

    dependencies {
        implementation 'org.slf4j:slf4j-api:1.7.24'
        implementation 'ch.qos.logback:logback-classic:1.2.1'
        implementation 'ch.qos.logback:logback-core:1.2.1'
        implementation 'com.google.code.findbugs:annotations:3.0.1u2'
        testImplementation 'junit:junit:4.13.2'
    }

    compileJava {
        sourceCompatibility '1.8'
        targetCompatibility '1.8'
        options.debug = true
        options.debugOptions.debugLevel = 'source,vars,lines'
        options.compilerArgs << "-Xlint:deprecation" << '-Xlint:unchecked'
    }
}
